input = syntax*
      ;

syntax = ID '=' body_list ';'
       | '%' ID* ';'
       ;

body_list = body* ('|' body*)*
          ;

body = '(' body_list ')' body_opt*
     | (ID | REGEXP) body_opt*
     ;

body_opt = /[?+*]/
         | '{' NUM (',' NUM)? '}'
         ;

NUM = '0'
    | /[1-9][0-9]*/
    ;

ID = /[a-zA-Z_][0-9a-zA-Z_]*/
   ;

REGEXP = '/' regexp_body* '/'
       ;

regexp_body = '[' ( '^' /./ | /[^^]/ ) /[^]]/* ']'
            | '(' regexp_body* ')'
            | '\\' /./
            | /[^[\\/()]/
            ;
